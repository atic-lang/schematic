use atic.schematic.Path;
use atic.schematic.Atic;
use atic.schematic.type.StaticTypes;
use atic.schematic.tokens.Node;
use atic.schematic.Context;

enum Type {
    Struct(StructImpl);
    Enum(EnumImpl);
    Function(FunctionType);
    Generic(GenericType);
    Array(Type);
    Primitive(string);
}


struct StructImpl {
    type: Struct;
    implementation: List<Type>
}

struct EnumImpl {
    type: Enum;
    implementation: List<Type>
}

struct FunctionType {
    inputs: List<Type>;
    output: Type;
    generics: Generics;
}

struct GenericType {
    index: number;
    name: string;
}


fn typeFromTree(node: Node, context: Context) -> Type {
    assertType(node, "type");
    let child = child(node);
    let name = child.name;
    if name == "classType"  {
        let genericsType: GenericsType = genericsTypeFromNode(getNode(child, "genericsOptionalType"), context);
        let path = newPathNode(getNode(child, "nChain"));
        if path.path == "void" {
            return Type.Primitive("void");
        } else if path.path == "number" {
            return Type.Primitive("number");
        } else if path.path == "bool" {
            return Type.Primitive("bool");
        } else if path.path == "string" {
            return Type.Primitive("string");
        }
        let genericResult = askForGeneric(context, path);
        if genericResult matches Some(generic) {
            return Type.Generic(generic);
        } else {
            let result = askFor(context, path);
            if result matches None() {
                  throw AssertError("cant find anything for " + path.path);
            } else matches Some(search) {
                if search matches Interface(struct_) {
                    return Type.Struct(StructImpl(struct_, genericsType.types));
                } else if search matches Enum(enum_) {
                    return Type.Enum(EnumImpl(enum_, genericsType.types));
                } else {
                    throw AssertError("Path " + path.path + " cant be resolved to a type");
                }
            }
        }
    } else if name == "functionType" {
        let list: List<Type> = typeListFromTree(getNode(child, "typeList"), context);
        let returnType: Type = typeFromTree(getNode(child, "type"), context);
        let generics: Generics = Generics(newList<GenericType>());
        return Type.Function(FunctionType(list, returnType, generics));
    } else if name == "arrayType" {
        return Type.Array(typeFromTree(getNode(child, "type"), context));
    } else  {
        throw AssertError("cant resolve type " + name);
    }
}

fn typeListFromTree(node: Node, context: Context) -> List<Type> {
    assertType(node, "typeList");
    let list = newList<Type>();
    forEach(walk("type", "typeList", node), fn (subNode: Node) -> void  {
        let type: Type = typeFromTree(subNode,context);
        insert(list,type);
    });
    return list;
}

fn substitute(type: Type, lookup: Map<GenericType, Type>) -> Type {
    if type matches Struct(structImpl) {
        let list = newList<Type>();
        forEach(structImpl.implementation, fn (implType: Type) -> void {
            insert(list, substitute(implType, lookup));
        });
        return Type.Struct(StructImpl(structImpl.type, list));
   } else if type matches Enum(enumImpl) {
        let list = newList<Type>();
        forEach(enumImpl.implementation, fn (implType: Type) -> void {
            insert(list, substitute(implType, lookup));
        });
        return Type.Enum(EnumImpl(enumImpl.type, list));
   } else if type matches Function(functionType) {
       throw AssertError("function for resolving is not impl");
   } else if type matches Array(element) {
       return Type.Array(substitute(element, lookup));
   } else if type matches Generic(genericType) {
       let result = get(lookup, genericType);
       if result matches Some(res) {
           return res;
       } else {
           return type;
       }
   } else if type matches Primitive(str) {
       return type;
   }
   throw AssertError(toString(type) + " is not impl");
}

fn typeToSting(type: Type) -> string {
    if type matches Struct(structImpl) {
        return "struct " + structImpl.type.path.path;
    } else if type matches Enum(enumImpl) {
        return "enum " + enumImpl.type.path.path;
    } else if type matches Function(functionType) {
        return "function ";
    } else if type matches Array(element) {
        return "Array of " + typeToSting(element);
    } else if type matches Generic(genericType) {
        return "Generic " + genericType.name;
    } else if type matches Primitive(str) {
        return "primitive " + str;
    }
    throw AssertError(toString(type) + " is not impl");
}

fn getMembers(structImpl: StructImpl) -> List<BIEntry<string,Type>> {
    let members = newList<BIEntry<string,Type>>();
    let lookup = createLookup(structImpl);
    forEach(structImpl.type.members, fn (entry: BIEntry<string,Type>) -> void {
        let type: Type = entry.value;
        let name: string = entry.key;
        let subType: Type = substitute(type, lookup)
        insert(members, BIEntry(name, subType));
    });
    return members;
}

fn createLookup(structImpl: StructImpl) -> Map<GenericType, Type> {
    let map = newMap<GenericType, Type>();
    let listA = structImpl.type.generics.types;
    let listB = structImpl.implementation;
    if listA.size != listB.size {
        throw AssertError("incompatible generic size");
    }
    let i: number = 0;
    let s: number = listA.size;
    while i < s {
        let implType = getAt(listB,i);
        let next = getAt(listA,i);
        put(map, next, implType);
        i = i + 1;
    }

    return map;
}